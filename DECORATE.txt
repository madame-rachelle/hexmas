Actor TreeSnowShort 130
{
	//$category "Obstacles"
	Radius 22
	Height 70
	+SOLID
	States
	{
		Spawn:
		TRE7 A -1
		Stop
	}	
}

Actor TreeSnowTall 131
{
	//$category "Obstacles"
	Radius 22
	Height 85
	+SOLID
	States
	{
		Spawn:
		TRE8 A -1
		Stop
	}	
}

Actor ArcherKnight 129
{
	//$category "Monsters"
    Health 160
    Radius 20
    Height 56
    Speed 13
    PainChance 200
	Mass 200
    MONSTER 
    +FLOORCLIP
	+MISSILEMORE
    SeeSound "archer/sight"
    AttackSound "archer/attack"
    PainSound "archer/pain"
	DeathSound "archer/death"
    ActiveSound "archer/growl"
    Obituary "%o was pierced by a Knight Archer."
    States
    {
    Spawn:
        KNIG AB 10 A_Look
        Loop
    See:
        KNIG AABBCCDD 3 A_Chase
        Loop
    Missile:
		KNIG E 0 A_Jump(64,"Missile2")
        KNIG E 3 A_PlaySound("archer/draw")
        KNIG E 10 fast slow A_FaceTarget
        KNIG F 5 bright A_CustomMissile("ArcherArrow",32,5)
		KNIG E 5
        Goto See
	Missile2:	
        KNIG E 3 A_PlaySound("archer/draw")
        KNIG E 10 fast slow A_FaceTarget
        KNIG G 5 bright A_CustomMissile("ArcherArrow2",32,5)
		KNIG E 5
        Goto See
    Pain:
    	KNIG H 3
        KNIG H 3 A_Pain
    	Goto See
    Death:
        KNIG I 5 A_Scream
        KNIG J 5 A_NoBlocking
        KNIG KLMN 5
        KNIG N -1
        Stop
    XDeath:
		KNIG O 5 A_PlaySound("EttinDeath")
		//KNIG O 5 A_PlaySound("archer/gib")
        KNIG P 5 A_NoBlocking
        KNIG QRSTUVWX 5
        KNIG X -1
        Stop
    }
}


actor ArcherArrow
{
   Radius 5
   Height 12
   Speed 24
   Damage 3
   PROJECTILE
   +NOGRAVITY
   renderstyle add
   alpha 0.8
   scale 0.5
   SeeSound "archer/attack"
   DeathSound "archer/hit"
   States
   {
   Spawn:
      VEKF A 1 bright A_SpawnItem("ArcherArrowTrail")
      Loop
   Death:
      VEKF FGHI 3 bright
	Stop
   }
}

actor ArcherArrowTrail
{
	+NOCLIP
	+NOBLOCKMAP
	+NOGRAVITY
	renderstyle add
	alpha 0.5
	States
	{
		Spawn:
		VEKF AABCDE 3 A_FadeOut(0.1)
		loop
	}
}

actor ArcherArrow2 : ArcherArrow
{
   Speed 30
   Damage 6
   SeeSound "archer/attackboss"
   States
   {
   Spawn:
      VEKG A 1 bright A_SpawnItem("ArcherArrowTrail2")
      Loop
   Death:
      VEKG FGHI 3 bright
	Stop
   }
}

actor ArcherArrowTrail2 : ArcherArrowTrail
{
	States
	{
		Spawn:
		VEKG AABCDE 3 A_FadeOut(0.1)
		loop
	}
}

Actor IceShard2
{
	Radius 0
	Height 0
	Damage 0
	Projectile
	DeathSound "MageShardsExplode"
	Renderstyle Translucent
	Alpha 0.5
	-SOLID
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetScale(frandom(0.1, 0.6))
		SHRD ABC 3 Bright
		SHRD ABC 3 Bright
		SHRD ABC 3 Bright A_Jump (64, "Death")
		SHRD ABC 3 Bright A_Jump (128, "Death")
		SHRD ABC 3 Bright A_Jump (192, "Death")
		SHRD ABC 3 Bright A_Jump (256, "Death")
		Death:
		SHRD ABC 3 Bright A_FadeOut(1)
		Stop
	}
}